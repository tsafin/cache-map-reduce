<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Sample.RemoteProxy">
<Description>
sample of a remote proxy using %Net.RemoteConnection</Description>
<Super>%RegisteredObject</Super>
<TimeCreated>64112,53523.232695</TimeCreated>

<Property name="Config">
<Type>%Object</Type>
<Internal>1</Internal>
</Property>

<Property name="RemoteConnection">
<Type>%Net.RemoteConnection</Type>
<Internal>1</Internal>
</Property>

<Property name="LastStatus">
<Type>%Status</Type>
<InitialExpression>$$$OK</InitialExpression>
</Property>

<Method name="%OnNew">
<FormalSpec>initval</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim retValue As %Status = $$$OK
    
    set ..RemoteConnection = ##class(%Net.RemoteConnection).%New()
    
    return retValue
]]></Implementation>
</Method>

<Method name="%CreateInstance">
<Description>
create new instance of a given class name</Description>
<FormalSpec>className:%String</FormalSpec>
<ReturnType>Sample.RemoteProxy.Object</ReturnType>
<Implementation><![CDATA[
    #dim instanceProxy As Sample.RemoteProxy.Object = ##class(Sample.RemoteProxy.Object).%New($this)
    return instanceProxy.%CreateInstance(className)
]]></Implementation>
</Method>

<Method name="%OpenObjectId">
<FormalSpec>className:%String,Id:%String</FormalSpec>
<ReturnType>Sample.RemoteProxy.Object</ReturnType>
<Implementation><![CDATA[
    #dim instanceProxy As Sample.RemoteProxy.Object = ##class(Sample.RemoteProxy.Object).%New($this)
    return instanceProxy.%OpenObjectId(className, Id)
]]></Implementation>
</Method>

<Method name="Debug">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
    set w = ##class(Sample.RemoteProxy).%New().%Connect({"Namespace":"SAMPLES", "Username":"_SYSTEM", "Password":"SYS"})
    //do w."Sample.Person::PrintPersons"(1)
    write w."Cinema.Duration::LogicalToDisplay"(200)
    set p = w.%OpenObjectId("Sample.Person",1)
    write p.Name
    write p.SSN
    write p.Addition(1,2)
]]></Implementation>
</Method>

<Method name="%Connect">
<Description>
pass the configuration object { "IP": IP, "Namespace" : Namespace, ... }</Description>
<FormalSpec>Config:%Object</FormalSpec>
<ReturnType>Sample.RemoteProxy</ReturnType>
<Implementation><![CDATA[
    #dim sIP As %String = Config.$get("IP")
    #dim sNamespace As %String = Config.$get("Namespace")
    #dim sPort As %String = Config.$get("Port")
    #dim sUsername As %String = Config.$get("Username")
    #dim sPassword As %String = Config.$get("Password")
    #dim sClientIP As %String = Config.$get("ClientIP")
    #dim sClientPort As %String = Config.$get("ClientPort")
    
    if sIP = "" {
        set sIP = "127.0.0.1"
    }
    if sPort = "" {
        set sPort = ^%SYS("SSPort")
    }
    set ..LastStatus = ..RemoteConnection.Connect(sIP, sNamespace, sPort, 
                                                  sUsername, sPassword, 
                                                  sClientIP, sClientPort)
    
    return $this
]]></Implementation>
</Method>

<Method name="ApparentlyClassName">
<Internal>1</Internal>
<ClassMethod>1</ClassMethod>
<FormalSpec>CompoundName:%String,*ClassName:%String,*MethodName:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
    #dim returnValue As %Boolean = 0
    
    if $length(CompoundName, "::") > 1 {
        set ClassName = $piece(CompoundName, "::", 1)
        set MethodName = $piece(CompoundName, "::", 2, *)

        return 1
    } elseif $length(CompoundName, "'") > 1 {
        set ClassName = $piece(CompoundName, "'", 1)
        set MethodName = $piece(CompoundName, "'", 2, *)

        return 1
    }

    return 0
]]></Implementation>
</Method>

<Method name="%DispatchMethod">
<Description>
log arguments and then dispatch dynamically method to the proxy object</Description>
<FormalSpec>methodName:%String,args...</FormalSpec>
<Implementation><![CDATA[
    //do ..LogArgs(methodName, args...)
    #dim className as %String = ""
    
    if ..ApparentlyClassName(methodName, .className, .methodName) {
        return ..InvokeClassMethod(className, methodName, args...)
    }
    return 1 ;$method(..OpenedObject, methodName, args...) // FIXME - no way to get here
]]></Implementation>
</Method>

<Method name="InvokeClassMethod">
<FormalSpec>ClassName:%String,MethodName:%String,args...</FormalSpec>
<Implementation><![CDATA[
    #dim returnValue = ""
    #dim i as %Integer
    do ..RemoteConnection.ResetArguments()
    for i=1:1:$get(args) {
        set ..LastStatus = ..RemoteConnection.AddArgument(args(i), 0)
    }
    set ..LastStatus = ..RemoteConnection.InvokeClassMethod(ClassName, MethodName, .returnValue, $quit)
    return returnValue
]]></Implementation>
</Method>
</Class>
</Export>
